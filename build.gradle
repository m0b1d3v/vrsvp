import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {

	id 'application'
	id 'jacoco'

	id 'com.github.ben-manes.versions' version '0.46.0'
	id 'io.freefair.lombok' version '8.0.1'
	id 'org.owasp.dependencycheck' version '8.2.1'
	id 'org.sonarqube' version '4.2.0.3129'
}

repositories {
	mavenCentral()
}

group = 'com.mobiusk.vrsvp'
version = '1.2'
description = 'Virtual RSVP system for Discord-based communities.'

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

application {
	mainClass.set('com.mobiusk.vrsvp.Main')
}

dependencies {

	// Discord bot
	implementation('net.dv8tion:JDA:5.0.0-beta.9') {
		exclude module: 'opus-java'
	}

	// Logging
	implementation 'ch.qos.logback:logback-classic:1.4.7'

	// Used for JSON formatting of log messages
	implementation 'net.logstash.logback:logstash-logback-encoder:7.3'

	// Exception monitoring
	implementation 'io.sentry:sentry:6.21.0'

	// Unit tests
	testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0-M1'
	testImplementation 'org.mockito:mockito-junit-jupiter:5.3.1'
}

dependencyCheck {
	analyzers {
		assemblyEnabled = false
	}
}

test {

	useJUnitPlatform()

	finalizedBy jacocoTestReport

	testLogging {
		events = [
			TestLogEvent.PASSED,
			TestLogEvent.SKIPPED,
			TestLogEvent.FAILED,
		]
	}
}

jacocoTestReport {

	dependsOn test

	reports {
		xml.required = true
	}
}

sonarqube {
	properties {
		property('sonar.host.url', 'https://sonarcloud.io')
		property('sonar.organization', 'mobiusk')
		property('sonar.projectKey', 'mobiusk_vrsvp')
	}
}
