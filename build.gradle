import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {

	id 'application'
	id 'jacoco'
	id 'java'

	id 'com.github.ben-manes.versions' version '0.46.0'
	id 'com.github.johnrengelman.shadow' version "8.1.1"
	id 'org.owasp.dependencycheck' version '8.2.1'
	id 'org.sonarqube' version '4.0.0.2929'
}

repositories {
	mavenCentral()
}

group = 'com.mobiusk.vrsvp'
version = '1.0-SNAPSHOT'
description = 'Virtual RSVP system for Discord-based communities.'

java.sourceCompatibility = JavaVersion.VERSION_17
java.targetCompatibility = JavaVersion.VERSION_17

application {
	mainClassName 'com.mobiusk.vrsvp.HelloWorld'
}

dependencies {

	implementation 'io.javalin:javalin-bundle:5.5.0'

	testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'
}

dependencyCheck {
	analyzers {
		assemblyEnabled = false
	}
}

test {

	useJUnitPlatform()

	finalizedBy jacocoTestReport

	testLogging {
		events = [
			TestLogEvent.PASSED,
			TestLogEvent.SKIPPED,
			TestLogEvent.FAILED,
		]
	}
}

jacocoTestReport {

	dependsOn test

	reports {
		xml.required = true
	}
}

sonarqube {
	properties {
		property('sonar.host.url', 'https://sonarcloud.io')
		property('sonar.organization', 'mobiusk')
		property('sonar.projectKey', 'mobiusk_vrsvp')
	}
}
